SigninLogs
| where ResultType == 0 // Filter to sign-ins that are successful. No interrupts
| where AADTenantId == ResourceTenantId // Filter out cross tenant resource access.
| where array_length(ConditionalAccessPolicies) != 0 //bootstrap
| where '*' in ({Users}) or UserDisplayName in ({Users})
| where ("{FilterDQ}" == "Yes" and ResourceIdentity !in ("00000002-0000-0000-c000-000000000000", "00000003-0000-0000-c000-000000000000")) or ("{FilterDQ}" != "Yes")
| where AppDisplayName in ({Apps}) or '*' in ({Apps})
| where '*' in ({DeviceState}) or case(DeviceDetail["trustType"] == "", "Unmanaged", DeviceDetail["trustType"]) in ({DeviceState})
| where ("{FilterLegacy}" == "Only Legacy Authentications" and (ClientAppUsed != "Browser" and ClientAppUsed != "Mobile Apps and Desktop clients")) or ("{FilterLegacy}" != "Only Legacy Authentications")
| where ("{TrustedLocation}" == "Only Trusted Locations" and NetworkLocationDetails contains "trustedNamedLocation") or ("{TrustedLocation}" != "Only Trusted Locations")
| extend UserDisplayName = strcat(UserDisplayName, " (", UserPrincipalName, ")")
| project UserDisplayName, UserType, AppDisplayName, AuthenticationRequirement
| where AuthenticationRequirement == "singleFactorAuthentication"
| summarize NotMfaCount=count() by AppDisplayName, UserDisplayName, UserType
| order by NotMfaCount desc
